plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "io.freefair.lombok" version "8.3"
}

group = 'me.lidan'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = 'https://jitpack.io' }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url 'https://mvn.lumine.io/repository/maven-public/' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT")
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation ('dev.triumphteam:triumph-gui:3.1.2') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation 'com.github.Revxrsal.Lamp:common:3.2.1'
    implementation 'com.github.Revxrsal.Lamp:brigadier:3.2.1'
    implementation 'com.github.Revxrsal.Lamp:bukkit:3.2.1'
    compileOnly 'com.github.tr7zw.Item-NBT-API:item-nbt-api-plugin:2.11.3'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly 'com.googlecode.json-simple:json-simple:1.1.1'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'io.lumine:Mythic-Dist:5.3.5'
    compileOnly 'me.clip:placeholderapi:2.11.6'
}

compileJava { // Preserve parameter names in the bytecode
    options.compilerArgs += ["-parameters"]
}

shadowJar{
    archiveClassifier = null
    relocate 'revxrsal.commands', 'me.lidan.cavecrawlers.lamp'
}

jar.enabled = false

build.dependsOn(shadowJar)

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}